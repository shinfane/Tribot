// A few things we need to know
// wheel diameter 5.6 cm.
// Full Wheel rotation: 17,6 cm
// Degrees per cm: 20,45
// Bot in place turn circle circumference: PI X 11,5cm = 36 cm.
// This means 1 Degree / mm.
//
// So turning 10 degrees eastwards: rotate the motors (turn mode) by 20 degrees.

#define THRESHOLD 25
#define NEAR 29 //cm
#define MIC SENSOR_2
#define RIGHT 1 
#define LEFT 0
#define FILENAME "track.txt"

// decided to use mutexes, just to make sure that
//  tasks do not impact each other

mutex MotorMutex_B;
int heading;
string print_heading;

// File handling stuff

byte fileHandle;
short fileSize=512;
short bytesToFile;
string write,read;





int read_compass()
{
	int i;
	
	i=SensorUS(IN_1);
	heading=i*360/255;
	Wait(20);
	return heading;
}


void drive_backward()
{
	int backsignal;
	string tmp;

	// turn Sensor backwards
	
	Acquire(MotorMutex_B);
	RotateMotor(OUT_B,25,180);
	Release(MotorMutex_B);
	
	// read sensor
	
	backsignal=SensorUS(S4);
	tmp=NumToStr(backsignal);
	
	TextOut(0,LCD_LINE1,"Back:");
	TextOut(50,LCD_LINE1,tmp);
	
	// if no obstacle, 
	if (backsignal>NEAR)
	{
		Wait(800);
		OnRev(OUT_AC,50); 
		Wait(500);
		Off(OUT_AC);
		
	}
	
	Acquire(MotorMutex_B);
	RotateMotor(OUT_B,25,-180);
	Release(MotorMutex_B);
	
	
	
}


// Turn to each side
// and check distance

int check_sides()
{
    int left_signal;
    int right_signal;
	string tmp;
	
    
    Wait(1000);
	Acquire(MotorMutex_B);
	
	// turn motor to the left and check US

    RotateMotor(OUT_B,25,90);
    left_signal=SensorUS(S4);
	
	// print the sensor reading
	
    TextOut(0,LCD_LINE2,"LEFT:");
	tmp=NumToStr(left_signal);
	TextOut(50,LCD_LINE2,tmp);
	
	Wait(1000);
	
	// turn motor to the right and check US
	
    RotateMotor(OUT_B,25,-180);
    right_signal=SensorUS(S4);
    
	// print the sesor reading

	TextOut(0,LCD_LINE4,"RIGHT:");
	tmp=NumToStr(right_signal);
	TextOut(50,LCD_LINE4,tmp);
	
	Wait(1000);
	
	// rotate back to the front
    RotateMotor(OUT_B,25,90);
	
	Release(MotorMutex_B);
    Wait(1000);
	

    if (left_signal > right_signal)
    {
        return LEFT;
    }
    else
    {
		return RIGHT;
    }
    
    
}


task main(){
    //    SetSensorLight(IN_3);
    SetSensorLowspeed(IN_4);
	SetSensorLowspeed(IN_1);
	
    int heading;
	
    //    RotateMotor(OUT_A,75,60);
    //    Off(OUT_A);
    //    RotateMotor(OUT_A,75,-60);
    //    Off(OUT_A);
    while(true){
        
		  
        OnFwd(OUT_AC,50);
        while(SensorUS(S4)>NEAR);
		
        Off(OUT_AC);
		
		// Clean up the display
		ClearScreen();
		
		heading=read_compass();
		
		TextOut(0,LCD_LINE5,"HEADING:");
		NumOut(50,LCD_LINE5, heading);
		
		drive_backward();
       
        if (check_sides()==LEFT)
        {
            OnFwd(OUT_A,50);
            OnRev(OUT_C,50);
            
        } 
        else
        {
            OnFwd(OUT_C,50);
            OnRev(OUT_A,50);
            
        }
        
        Wait(500);
        
        Off(OUT_AC);
        Wait(2000);
    }
}

